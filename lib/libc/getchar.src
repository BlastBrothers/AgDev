; Changed to remove bug of typecasting from singed char to int - should be from unsigned
; Paul Cawte 03/06/2023

; int getchar(void)
;
; is a standard library function that takes a single input character from standard input.
; The major difference between getchar and getc is that getc can take input from any no of
; input streams but getchar can take input from a single standard input stream.
;
; Parameters:
; None.
; Return Value:
; The input from the standard input is read as an unsigned char and then it is typecast
; and returned as an integer value(int).
; EOF is returned in two cases:
; - When the file end is reached
; - When there is an error during the execution

; BUG warning
; typecasts from signed char, not unsigned char - behaviour left unchanged

	assume	adl=1

	include	"stdio.inc"

	section	.text
	public	_getchar
_getchar:
	ld 	iy,_stdin 		; FILE* pointer for stdin
	ld 	a,(iy+FILE_FH)		; check if stdim still refers
	cp 	a,FH_STDIN 		; to the default (console)
	jr	nz,_call_fgetc

	call	_inchar			; get character from OS - in A
	or 	a			; clear carry flag
	sbc	hl,hl 			; HL = 0 
	ld	l,a 			; typecast unsigned char (A) to int (HL)

_echo_char:
	push	hl
	call	_outchar		; echo the character to stdout
	pop	hl
	ret

_call_fgetc:				
	push	iy 			; push the FILE* pointer parameter
	call 	_fgetc 			; int fgetc(FILE *stream), return value in HL
	pop	de 			; clear up the stack
	ret 				; in this case we do not echo the character

	extern	_inchar
	extern	_outchar
	extern 	_stdin
	extern	_fgetc
